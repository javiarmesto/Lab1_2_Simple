namespace YourCompany.VendorCopilot;
using System.AI;

/// <summary>
/// P√°gina principal del asistente Copilot para gesti√≥n de proveedores
/// Permite generar descripciones, categor√≠as, t√©rminos de pago y checklists
/// En realidad, esta p√°gina es un PromptDialog que interact√∫a con Azure OpenAI.
/// El usuario puede introducir informaci√≥n del proveedor y seleccionar el tipo de asistencia deseada.  
/// El contenido generado por IA se muestra en un campo de texto enriquecido.
/// Esta p√°gina no tiene una SourceTable f√≠sica, ya que es un PromptDialog.
/// El usuario puede confirmar o descartar los cambios generados por IA.
/// Pero no guardaremos nada, no hemos a√±adido la l√≥gica para ello, pero recordar que debemos usar tablas temporales por Responsabilidad AI
/// Esta p√°gina es un ejemplo de c√≥mo integrar IA en la gesti√≥n de proveedores en Business Central.
/// 
page 50100 "Vendor Copilot Assistant"
{
    PageType = PromptDialog;
    ApplicationArea = All;
    Caption = 'Vendor Copilot Assistant';
    PromptMode = Prompt;
    //SourceTable = "Copilot XXX temporal";// No se usa SourceTable f√≠sico, ya que es un PromptDialog
    Extensible = false;

    layout
    {
        area(Prompt)
        {
            field(VendorInfoField; InputVendorInfo)
            {
                ShowCaption = false;
                MultiLine = true;
                InstructionalText = 'Introduce informaci√≥n b√°sica del proveedor (nombre, industria, servicios, ubicaci√≥n, etc.)';
            }
        }

        area(PromptOptions)
        {
            field(AssistanceType; AssistanceTypeOption)
            {
                Caption = 'Tipo de Asistencia';
                ToolTip = 'Selecciona qu√© tipo de ayuda necesitas para este proveedor';
                OptionCaption = 'Descripci√≥n Completa,Categorizaci√≥n,T√©rminos de Pago,Checklist Onboarding';
            }
        }

        area(Content)
        {
            field("Generated Content"; OutputContent)
            {
                Caption = 'Contenido Generado por IA';
                MultiLine = true;
                ExtendedDatatype = RichContent;
                Editable = false;
            }
        }
    }

    actions
    {
        area(SystemActions)
        {
            // Puedes tener un comportamiento personalizado para las acciones principales del sistema en una p√°gina de tipo PromptDialog, como generar una sugerencia con Copilot, regenerar o descartar la sugerencia.
            // Cuando desarrolles una funcionalidad de Copilot, recuerda: el usuario siempre debe tener el control (el usuario debe confirmar cualquier cosa que Copilot sugiera antes de que se guarde alg√∫n cambio).
            // Esta es tambi√©n la raz√≥n por la cual no puedes tener una SourceTable f√≠sica en una p√°gina de tipo PromptDialog (debes usar una tabla temporal o ninguna tabla).
            systemaction(Generate)
            {
                Caption = 'Generar con Copilot';
                ToolTip = 'Usa IA para generar contenido del proveedor';

                trigger OnAction()
                var
                    SystemPrompt: Text;
                begin
                    if InputVendorInfo = '' then begin
                        Message('Por favor, introduce informaci√≥n del proveedor.');
                        exit;
                    end;

                    SystemPrompt := BuildSystemPrompt();
                    OutputContent := CreateSuggestion(SystemPrompt, InputVendorInfo);
                end;
            }
            //Importante: En un PromptDialog, las acciones de sistema como "Generate", "Regenerate" y "Discard" no tienen un SourceTable asociado, ya que no se guardan cambios directamente en una tabla.
            systemaction(OK)
            {
                Caption = 'Confirmar';
                ToolTip = 'Confirmar y guardar cambios';
            }
            systemaction(Cancel)
            {
                Caption = 'Descartar';
                ToolTip = 'Descartar cambios y cerrar el asistente';
            }


        }

        area(PromptGuide)
        {
            action("TechVendor")
            {
                Caption = 'Proveedor Tecnol√≥gico';
                ToolTip = 'Ejemplo de proveedor de servicios tecnol√≥gicos';

                trigger OnAction()
                begin
                    InputVendorInfo := 'TechSolutions SA - Empresa de desarrollo de software especializada en ERP y CRM. Ubicada en Madrid, 50 empleados, certificada ISO 27001. Servicios: desarrollo custom, consultor√≠a, soporte t√©cnico 24/7.';
                end;
            }

            action("LogisticsVendor")
            {
                Caption = 'Proveedor Log√≠stico';
                ToolTip = 'Ejemplo de proveedor de servicios log√≠sticos';

                trigger OnAction()
                begin
                    InputVendorInfo := 'LogiTrans Express - Empresa de transporte y log√≠stica. Opera a nivel nacional con flota de 200 veh√≠culos. Servicios: transporte urgente, almacenaje, distribuci√≥n last-mile, tracking en tiempo real.';
                end;
            }

            action("SupplierMaterial")
            {
                Caption = 'Proveedor de Materiales';
                ToolTip = 'Ejemplo de proveedor de materiales';

                trigger OnAction()
                begin
                    InputVendorInfo := 'Materiales Construcci√≥n L√≥pez - Distribuidor de materiales de construcci√≥n. 30 a√±os en el mercado, almac√©n de 5000m¬≤. Productos: cemento, hierro, herramientas, maquinaria pesada.';
                end;
            }
        }
    }

    var
        InputVendorInfo: Text;
        OutputContent: Text;
        AssistanceTypeOption: Option "Descripci√≥n Completa","Categorizaci√≥n","T√©rminos de Pago","Checklist Onboarding";

    /// <summary>
    /// Construye el prompt del sistema seg√∫n el tipo de asistencia seleccionado.
    /// Esta funci√≥n determina el mensaje de instrucciones (prompt) que se enviar√° a la IA,
    /// adaptando el contenido y formato seg√∫n la opci√≥n elegida por el usuario.
    /// El prompt gu√≠a a la IA para que genere una respuesta relevante y estructurada.
    /// </summary>
    local procedure BuildSystemPrompt(): Text
    var
        SystemPrompt: Text;
    begin
        case AssistanceTypeOption of
            AssistanceTypeOption::"Descripci√≥n Completa":
                SystemPrompt := 'Eres un experto en gesti√≥n de proveedores en Microsoft Dynamics 365 Business Central. ' +
                    'Genera una descripci√≥n completa y profesional del proveedor en formato HTML. ' +
                    'Incluye: descripci√≥n de servicios, fortalezas principales, consideraciones de riesgo, y recomendaciones de gesti√≥n. ' +
                    'Usa formato HTML con encabezados <h3>, listas <ul> y texto en <p>. S√© espec√≠fico y profesional.';

            AssistanceTypeOption::Categorizaci√≥n:
                SystemPrompt := 'Eres un especialista en categorizaci√≥n de proveedores para sistemas ERP. ' +
                    'Analiza la informaci√≥n y sugiere: categor√≠a principal, subcategor√≠a, c√≥digo de clasificaci√≥n sugerido, ' +
                    'nivel de criticidad (Alto/Medio/Bajo) y justificaci√≥n detallada. ' +
                    'Formato HTML con categor√≠as en <strong> y justificaciones en vi√±etas <ul>.';

            AssistanceTypeOption::"T√©rminos de Pago":
                SystemPrompt := 'Eres un experto en t√©rminos comerciales y gesti√≥n de cash flow. ' +
                    'Bas√°ndote en el tipo de proveedor y servicios, sugiere: ' +
                    't√©rminos de pago √≥ptimos, descuentos por pronto pago recomendados, ' +
                    'frecuencia de pago sugerida y consideraciones de riesgo financiero. ' +
                    'Incluye ejemplos de c√≥digos de t√©rminos de pago para Business Central (ej: 30 D√çAS, NET30, 2%10NET30).';

            AssistanceTypeOption::"Checklist Onboarding":
                SystemPrompt := 'Eres un consultor experto en onboarding de proveedores en entornos empresariales. ' +
                    'Crea un checklist detallado de onboarding espec√≠fico para este tipo de proveedor. ' +
                    'Incluye: documentaci√≥n requerida, verificaciones necesarias, ' +
                    'configuraciones en Business Central, responsables y timeline estimado. ' +
                    'Formato HTML con checkboxes (‚òê) y prioridades marcadas con emojis (üî¥ Alta, üü° Media, üü¢ Baja).';
        end;

        exit(SystemPrompt);
    end;

    /// <summary>
    /// Crea la sugerencia usando Azure OpenAI Service.
    /// Esta funci√≥n orquesta todo el proceso de generaci√≥n de contenido con IA:
    /// 1. Registra la capacidad de Copilot si no existe.
    /// 2. Configura la conexi√≥n a Azure OpenAI (endpoint, deployment, API key, modelo).
    /// 3. Define los par√°metros del modelo (m√°ximo de tokens, temperatura).
    /// 4. Prepara los mensajes del chat (sistema y usuario).
    /// 5. Llama al servicio de IA para obtener la respuesta.
    /// 6. Devuelve el contenido generado o un mensaje de error.
    ///
    /// Codeunits utilizadas:
    /// - CopilotCapability: Gestiona el registro y consulta de capacidades Copilot.
    /// - AzureOpenAI: Maneja la autenticaci√≥n y comunicaci√≥n con Azure OpenAI.
    /// - AOAIDeployments: Permite obtener informaci√≥n sobre los modelos y despliegues disponibles.
    /// - AOAIChatCompletionParams: Define los par√°metros de la solicitud al modelo (tokens, temperatura, etc.).
    /// - AOAIChatMessages: Gestiona los mensajes enviados y recibidos en la conversaci√≥n con la IA.
    /// - AOAIOperationResponse: Recoge el resultado y estado de la operaci√≥n de IA.
    /// </summary>
    local procedure CreateSuggestion(SystemPrompt: Text; VendorInfo: Text): Text
    var
        // Codeunit "Copilot Capability": Gestiona el registro y consulta de capacidades Copilot.
        CopilotCapability: Codeunit "Copilot Capability";
        // Codeunit "Azure OpenAI": Maneja la autenticaci√≥n y comunicaci√≥n con Azure OpenAI.
        AzureOpenAI: Codeunit "Azure OpenAI";
        // Codeunit "AOAI Deployments": Permite obtener informaci√≥n sobre los modelos y despliegues disponibles.
        AOAIDeployments: Codeunit "AOAI Deployments";
        // Codeunit "AOAI Chat Completion Params": Define los par√°metros de la solicitud al modelo (tokens, temperatura, etc.).
        AOAIChatCompletionParams: Codeunit "AOAI Chat Completion Params";
        // Codeunit "AOAI Chat Messages": Gestiona los mensajes enviados y recibidos en la conversaci√≥n con la IA.
        AOAIChatMessages: Codeunit "AOAI Chat Messages";
        // Codeunit "AOAI Operation Response": Recoge el resultado y estado de la operaci√≥n de IA.
        AOAIOperationResponse: Codeunit "AOAI Operation Response";
    begin
        // Registrar capacidad si no existe
        if not CopilotCapability.IsCapabilityRegistered(Enum::"Copilot Capability"::"Vendor Enhancement") then
            CopilotCapability.RegisterCapability(Enum::"Copilot Capability"::"Vendor Enhancement", 'https://about:none');

        // Configurar Azure OpenAI con managed resources (si se dispone de un recurso administrado)
        //AzureOpenAI.SetManagedResourceAuthorization(
        //    Enum::"AOAI Model Type"::"Chat Completions",
        //    GetEndpoint(),
        //    GetDeployment(),
        //    GetApiKey(),
        //    AOAIDeployments.GetGPT4oLatest()
        //);
        // If you are using your own Azure OpenAI subscription, call this function instead:
        AzureOpenAI.SetAuthorization(Enum::"AOAI Model Type"::"Chat Completions", GetEndpoint(), GetDeployment(), GetApiKey());
        AzureOpenAI.SetCopilotCapability(Enum::"Copilot Capability"::"Vendor Enhancement");

        // Configurar par√°metros del modelo
        AOAIChatCompletionParams.SetMaxTokens(2000);
        AOAIChatCompletionParams.SetTemperature(0.3); // Un poco de creatividad pero manteniendo precisi√≥n

        // Preparar mensajes del chat
        AOAIChatMessages.AddSystemMessage(SystemPrompt);
        AOAIChatMessages.AddUserMessage(VendorInfo);

        // Generar respuesta
        AzureOpenAI.GenerateChatCompletion(AOAIChatMessages, AOAIChatCompletionParams, AOAIOperationResponse);

        if AOAIOperationResponse.IsSuccess() then
            exit(AOAIChatMessages.GetLastMessage())
        else
            exit('‚ùå Error al generar contenido: ' + AOAIOperationResponse.GetError());
    end;

    // M√©todos de configuraci√≥n (usar valores dummy para demo con managed resources)
    local procedure GetApiKey(): SecretText
    begin
        // En producci√≥n real, usar Azure Key Vault o Isolated Storage (ver ejemplo en documentaci√≥n oficial)
        // Para este demo, usamos un API directamenten ya que es un laboratorio
        exit(Format('FnXj5Vh1LblqLKCXGRUtAdUsZl3wPPauYbZdpMibcksRg931taulJQQJ99BAACYeBjFXJ3w3AAABACOGuFRM'));
    end;

    local procedure GetDeployment(): Text
    begin
        // Para managed resources, el deployment name es manejado autom√°ticamente , aqui indicamos el modelo
        // En este caso, usamos gpt-4o como modelo en la suscripcion azure
        exit('gpt-4o');
    end;

    local procedure GetEndpoint(): Text
    begin
        // Para managed resources, el endpoint es manejado autom√°ticamente , aqui indicamos el endpoint
        // En este caso, usamos el endpoint de Azure OpenAI
        exit('https://circe.openai.azure.com/');
    end;

    /// <summary>
    /// M√©todo p√∫blico para pre-llenar informaci√≥n del proveedor (llamado desde extensiones)
    /// </summary>
    procedure SetVendorInfo(VendorName: Text; VendorSearchName: Text)
    begin
        InputVendorInfo := VendorName;
        if VendorSearchName <> '' then
            InputVendorInfo += ' - ' + VendorSearchName;
    end;
}